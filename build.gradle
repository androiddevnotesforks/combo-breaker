import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        compose_bom = '2023.01.00'
        core_ktx_version = '1.9.0'
        compose_compiler_version = '1.3.2'
    }

    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.24.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.7.20'
    }
}

plugins {
    id 'com.android.application' version '8.1.0-alpha10' apply false
    id 'com.android.library' version '8.1.0-alpha10' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.20' apply false
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("combo-breaker.enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/metrics"
                ]
            }
        }
    }
}
