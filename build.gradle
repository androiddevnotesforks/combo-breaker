import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        compose_bom = '2023.04.01'
        core_ktx_version = '1.10.0'
        compose_compiler_version = '1.4.6'
    }

    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.24.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.8.10'
    }
}

plugins {
    id 'com.android.application' version '8.1.0-alpha11' apply false
    id 'com.android.library' version '8.1.0-alpha11' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            if (project.findProperty("combo-breaker.enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/metrics"
                ]
            }
        }
    }
}
